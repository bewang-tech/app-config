buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "http://dl.bintray.com/ramonza/gradle-plugins"
        }
        jcenter()
    }
    dependencies {
        classpath "org.ajoberstar:grgit:1.3.0"
        classpath "com.rhapsody.bi:gradle-bi-plugin:0.0.1"
        classpath "cucumber.contrib.gradle:cucumber-gradle:0.2.0"
        classpath "com.github.maiflai:gradle-scalatest:0.12"
    }
}

plugins {
    id "scala"
    id "maven-publish"
    id "distribution"
    id "nebula.provided-base" version "3.0.3"
}

apply plugin: "cucumber"
apply plugin: "com.github.maiflai.scalatest"

sourceCompatibility = 1.7
targetCompatibility = 1.7

def scalaMajor = "2.10"
def scalaVersion = "2.10.4"

ext {
    git = org.ajoberstar.grgit.Grgit.open(currentDir: project.projectDir)
    revision = git.head().id
    gitVersion = git.describe().replaceFirst(/^(?i)v/, "")
    testWorkingDir = new File(buildDir, "test-tmp")
}

group = "com.rhapsody.bi"
version = gitVersion
description = "Typesafe Config Extension"

jar {
    manifest {
        attributes "Implementation-Title": description,
                "Implementation-Version": revision
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "http://maven01.internal.rhapsody.com/nexus/content/groups/public" }
    maven { url "http://rds-repo-dev-1201.sea2.rhapsody.com:8080/nexus/content/groups/public" }
    maven { url "http://rds-repo-dev-1201.sea2.rhapsody.com:8080/nexus/content/groups/public-snapshots" }
}

dependencies {
    compile "com.typesafe:config:1.2.1"
    compile "org.scala-lang:scala-library:$scalaVersion"
    compile "org.scala-lang:scala-reflect:$scalaVersion"

    testCompile "org.scalatest:scalatest_$scalaMajor:2.2.6"
    testCompile "org.scalacheck:scalacheck_$scalaMajor:1.12.5"

    testRuntime "org.pegdown:pegdown:1.1.0"

    testCompile "info.cukes:cucumber-scala_$scalaMajor:1.2.4"
    testCompile "info.cukes:cucumber-junit:1.2.4"
}

cucumber {
    systemProperty "mySystemProperty", "someValue"
    jvmArgs = ["-Xmx2G", "-XX:MaxPermSize=256M"]

    // The following are default the values:
    features += project.sourceSets.test.output.resourcesDir
    htmlReport true
    junitReport true
    progressOutput false
    prettyOutput true

    // monochrome defaults to use Gradle --color flag
    monochrome false
}

test.doFirst {
    testWorkingDir.mkdirs()
}

test {
    systemProperties "property": "value"
    maxParallelForks = 1
    workingDir testWorkingDir
}

task wrapper(type: Wrapper) { gradleVersion = "2.10" }

task scalaDocJar(type: Jar, dependsOn: scaladoc) {
    classifier = 'scaladoc'
    from scaladoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives scalaDocJar, sourcesJar
}

publishing {
    repositories {
        maven {
            name "rhapsody"
            url "http://maven01.internal.rhapsody.com/nexus/content/repositories/releases"
	    credentials {
	      username "$mavenUser"
	      password "$mavenPassword"
	    }
        }
    }
    publications {
        maven(MavenPublication) {
            from components.java
            artifact(scalaDocJar) {
                classifier = 'scaladoc'
            }
            artifact(sourcesJar) {
                classifier = 'sources'
            }
        }
    }
}
